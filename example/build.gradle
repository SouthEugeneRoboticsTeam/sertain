plugins {
    id "org.jetbrains.kotlin.jvm"
    id "edu.wpi.first.GradleRIO" version "2020.1.2"
}

ext.kotlinVersion = "1.3.71"

tasks.whenTaskAdded { task ->
    if (task.name == "deploy" || task.name == "deployMain" || task.name == "simulateJava") task.dependsOn "assemble"
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://first.wpi.edu/FRC/roborio/maven/release" }
    maven { url "https://frcmaven.wpi.edu/artifactory/release/" }
    maven { url "http://devsite.ctr-electronics.com/maven/release" }
    maven { url "https://www.kauailabs.com/maven2" }
    maven { url "http://www.revrobotics.com/content/sw/max/sdk/maven/" }
    maven { url "http://www.revrobotics.com/content/sw/color-sensor-v3/sdk/maven/" }
    google()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"
    implementation "com.kauailabs.navx.frc:navx-java:3.1.400"
    implementation "com.ctre.phoenix:api-java:5.14.1"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.61"
    implementation 'com.github.SouthEugeneRoboticsTeam:sertain:0.0.12'
    implementation wpi.deps.wpilib()
    implementation wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
    nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.wpilibJni(wpi.platforms.desktop)

    compile project(":core")
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
    kotlinOptions.freeCompilerArgs = [
            "-XXLanguage:+InlineClasses",
            "-Xuse-experimental=kotlin.Experimental",
            "-Xallow-result-return-type"
    ]
}

deploy {
    targets {
        roboRIO("roborio") {
            team = frc.getTeamOrDefault(2521)
        }
    }
    artifacts {
        frcJavaArtifact("frcJava") {
            targets << "roborio"
            debug = frc.getDebugOrDefault(false)
        }
        fileTreeArtifact("frcStaticFileDeploy") {
            files = fileTree(dir: "src/main/deploy")
            targets << "roborio"
            directory = "/home/lvuser/deploy"
        }
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes(
                "Main-Class": "org.sert2521.infiniterecharge2020.MainKt"
        )
    }
}
